maml = ws-comment-newline value ws-comment-newline

value =  object / array / string / multiline-string / number / "true" / "false" / "null"

object = "{" [ members ] ws-comment-newline "}"

members =  ws-comment-newline key-value ws [ comment ] separator members
members =/ ws-comment-newline key-value ws [ comment ] [ separator ]

key-value = key ws ":" ws value

key = string / identifier

identifier = 1*( ALPHA / DIGIT / "-" / "_" )

array = "[" [ items ] ws-comment-newline "]"

items =  ws-comment-newline value ws [ comment ] separator items
items =/ ws-comment-newline value ws [ comment ] [ separator ]

separator = "," / newline

string = quote *char quote

multiline-string = 3quote [ newline ] multiline-string-body 3quote

multiline-string-body = *literal-char *( allowed-quotes 1*literal-char ) [ allowed-quotes ]

allowed-quotes = 1*2quote

quote = %x22

literal-char = %x09 / %x20-21 / %x23-7E / non-ascii / newline

number = [ "-" ] integer [ fraction ] [ exp ]

onenine = %x31-39

exp = ( "e" / "E" ) [ "-" / "+" ] 1*DIGIT

fraction = "." 1*DIGIT

integer = "0" / ( onenine *DIGIT )

char =  %x20-21 / %x23-5B / %x5D-10FFFF
char =/ %x5C ( %x5C / quote / "/" / "b" / "f" / "n" / "r" / "t" / "u" 4HEXDIG )

comment = "#" *non-eol

non-eol = %x09 / %x20-7E / non-ascii

non-ascii = %x80-D7FF / %xE000-10FFFF

ws-comment-newline = *( (SP / HTAB) / [ comment ] newline )

newline = LF / CR LF

ws = *( SP / HTAB )

SP     = %x20              ; space
HTAB   = %x09              ; horizontal tab
CR     = %x0D              ; carriage return
LF     = %x0A              ; linefeed
ALPHA  = %x41-5A / %x61-7A ; A-Z / a-z
DIGIT  = %x30-39           ; 0-9
HEXDIG = DIGIT / "A" / "B" / "C" / "D" / "E" / "F"

