maml = space value space

value =  object / array / string / number / "true" / "false" / "null"

object = '{' [ members ] space '}'

members =  space key-value ws [ comment ] separator members
members =/ space key-value space [ separator ]

key-value = key ws ':' ws value

key = string / identifier

identifier = 1*( ALPHA / DIGIT / '-' / '_' )

array = '[' [ items ] space ']'

items =  space value ws [ comment ] separator items
items =/ space value space [ separator ]

separator = ',' / newline

string = '"' *char '"'

number = [ '-' ] integer [ fraction ] [ exp ]

onenine = %x31-39

exp = ( 'e' / 'E' ) [ '-' / '+' ] 1*DIGIT

fraction = '.' 1*DIGIT

integer = '0' / ( onenine *DIGIT )

char =  %x20-21 / %x23-5B / %x5D-10FFFF
char =/ '\' ( '"' / '\' / '/' / 'b' / 'f' / 'n' / 'r' / 't' / %x75 4HEXDIG )

space = *( ws [ comment ] )

comment = '#' *non-eol

non-eol = %x09 / %x20-7E / non-ascii

non-ascii = %x80-D7FF / %xE000-10FFFF

newline = LF / CRLF

ws = *( SP / HTAB / LF / CR )
